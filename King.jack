class King{
    field int maxOffset, maxWidth, maxHeight; //maxWidth is max number of locations
    field int prevX, prevY, prevOffset;
    field int currX, currY, currOffset;
    field int direction, prevDirection;
    field int dx, dy; 
    field int gravity, g, airTime;
    field int hitboxX, hitboxY, hitboxW, hitboxH;
    field boolean landed;
    field Array debug;


    constructor King new(int x, int y){
        /*
        location = y * 32 + x;
        0 <= x <= 32 
        0 <= y <= 255
        0 <= offset < 8 
        
        Idea with gravity:
        i want to reach max height in 1s => dy should be 0 that is
        dy = -gravity in 1s => dy = 2 and i keep track of airTime counter
        because gravity = (airTime / FPS) * GRAVITY => airTime / FPS = 1 when 
	    airTime = FPS which is in 1s of airTime
        */

        //initialze variables
        let currX = x;
        let currY = y;
        let currOffset = 0;

        //NOTE: currX nije prevX samo u jednom inputu
        let prevX = x;
        let prevY = y;
        let prevOffset = 0;
        
        let dx = 1;
        let dy = -4;
        let gravity = -dy + 1; //airTime = 48 => dy = g
        let g = 0;
        let airTime = 0;

        let prevDirection = 132;
        let direction = 132; //130 - left, 132 - right
        let landed = false;

        let maxOffset = 8;
        let maxHeight = 256;
        let debug = Array.new(10);

        do render();

        return this;
    }

    // setters
    method void setX(int x){
        let prevX = currX;
        let currX = Utils.mod(x, maxWidth); 
        return;
    }

    method void setY(int y){
        //Really wierd cases
        let prevY = currY;
        if(y < 30){
            //TODO: doenst work becasue eraseResidue erases legs and not whole king an not even
            // legs becasue moveSideways isn't called yet
            let currY = 256 - (30 - y);
            //let prevY = 255;
        }
        else{if(y > 255){
            let currY = 30 + (y - 255);
            //let prevY = 30;
            do Output.printInt(prevX);
            do Output.printInt(prevOffset);
        }else{
            //let prevY = currY;
            let currY = y;
        }}
        return;
    }

    method void setOffset(int offset){
        let prevOffset = currOffset;
        let currOffset = Utils.mod(offset, maxOffset); 
        return;
    }

    method void setDirection(int key){
        //prevDirection cannot be upward(131)
        //prevDirection is the one holding king's orientation that was
        //before this input
        if(~(direction = 131)){
            let prevDirection = direction;
        }
        let direction = key;
        return;
    }
    
    //getters
    method int getX()       { return currX; }
    method int getY()       { return currY; }
    method int getOffset()  { return currOffset; }
     
    method void refresh(int duration, int FPS){
        //NOTE: tracking inputs: on every refresh, update previous variables
        //      by calling for example setX(currX)
        if(duration > 0){
            //move up
            do moveVertical(FPS);
        }else{
            do setY(currY);
        }

        if(~(direction = 131)){
            do moveSideways();
        }else{
            do setX(currX);
            do setOffset(currOffset);
        }
        return;
    }

    method void moveVertical(int FPS){
        let g = (airTime * gravity) / FPS;
        do setY(currY + dy + g);
        let airTime = airTime + 1;
        return;
    }

    method void moveSideways(){
        do setX(currX);

        if(~(prevDirection = direction)){
            if(~(currOffset = 0)){
                if(direction = 132){
                    //let currX = Utils.mod(currX - 1, maxWidth);
                    do setX(currX - 1);
                }else{
                    //let currX = Utils.mod(currX + 1, maxWidth);
                    do setX(currX + 1);
                }
            }

            //apply symmetry to offset
            do setOffset(((maxOffset) - currOffset) + dx);
        }else{
            do setOffset(currOffset + dx);
        }

        
        if((currOffset = 0)){
            if(direction = 130){
                do setX(currX - 1);
            }else{
                do setX(currX + 1);
            }
        }

        return;
    }


    method void kingRender(){
        var int dir;

        let dir = direction;
        if(dir = 131){  
            let dir = prevDirection;
        }

        if((currOffset = 0) & ~(prevX = currX)){
            if(dir = 132){
                do Graphics.kingRight8(prevX, currY);
            }else{
                do Graphics.kingLeft8(prevX, currY);
            }
        }

        //only 0 offset requires previous position(only X)
        if((currOffset = 0) & (prevX = currX)){
            do Graphics.kingIdle(currX, currY);
        }

        if(currOffset = 1){
            if(dir = 132){
                do Graphics.kingRight1(currX, currY);
            }else{
                do Graphics.kingLeft1(currX, currY);
            }
        }

        if(currOffset = 2){
            if(dir = 132){
                do Graphics.kingRight2(currX, currY);
            }else{
                do Graphics.kingLeft2(currX, currY);
            }
        }

        if(currOffset = 3){
            if(dir = 132){
                do Graphics.kingRight3(currX, currY);
            }else{
                do Graphics.kingLeft3(currX, currY);
            }
        }

        if(currOffset = 4){
            if(dir = 132){
                do Graphics.kingRight4(currX, currY);
            }else{
                do Graphics.kingLeft4(currX, currY);
            }
        }

        if(currOffset = 5){
            if(dir = 132){
                do Graphics.kingRight5(currX, currY);
            }else{
                do Graphics.kingLeft5(currX, currY);
            }
        }

        if(currOffset = 6){
            if(dir = 132){
                do Graphics.kingRight6(currX, currY);
            }else{
                do Graphics.kingLeft6(currX, currY);
            }
        }

        if(currOffset = 7){
            if(dir = 132){
                do Graphics.kingRight7(currX, currY);
            }else{
                do Graphics.kingLeft7(currX, currY);
            }
        }

        return;
    }
    
    
    //erase residue(ostatak) with rectangles
    method void eraseResidue(){
        var int x1,y1,x2,y2;

        do Screen.setColor(0);
        if(~(currX = prevX) & (~(direction = prevDirection)) & (currOffset = 0)){
            //this condition is done only for kingRender!
            //just change prevX instead of adding new variable becasue its at the end of
            //a frame and prevX is not used in calculating next ones
            let prevX = currX;
        }

        if(prevDirection = 132){
            let x1 = prevX * 16 + (prevOffset * 2);
            let x2 = (prevX + 2) * 16 + (prevOffset * 2);
        }else{
            let x1 = (prevX * 16) - (prevOffset * 2); 
            let x2 = (prevX + 2) * 16 - (prevOffset * 2) - 3;
        }
            
        if(~(g = Math.abs(dy))){


            let y1 = currY + 1;
            let y2 = prevY;
            
            //erase head residue
            if(g > Math.abs(dy) & (y1 > 30)){
                //king height: 29px 
                //prevY, currY is for his legs
                //have to be reversed so (x1,y1) is topLeft
                let y1 = prevY - 30;
                let y2 = currY - 30;
            }

            if(Math.abs(prevY - currY) > 100){
                let y1 = prevY - 30;
                let y2 = prevY;
            }

            //NOTE: 2 rectangles needed for switching sides
            if(x2 > (511)){
                do Screen.drawRectangle(0, y1, x2 - 511, y2);
            }
            if(x1 < 0){
                do Screen.drawRectangle(511 + x1, y1, 511, y2);
            }

            let x1 = Utils.max(x1, 0);
            let x2 = Utils.min(x2, 511);

            do Screen.drawRectangle(x1,y1,x2,y2);
            
        }
        //do Sys.wait(2000);
        return;
    }   

    method void render(){
        do kingRender();
        //horizonal residue is erased with next draw function
        if(~(prevY = currY)){
            do eraseResidue();
        }
        return;
    }

    method void computeHitbox(){
        var int dir;
        
        let dir = direction;
        if(direction = 131){
            let dir = prevDirection;
        }
        if(dir = 132){
            let hitboxX = (currX * 16) + (currOffset * 2) + 2;
            let hitboxY = currY - 29;
            let hitboxW = 24;
            let hitboxH = 29;   
        }else{
            let hitboxX = (currX * 16) - (currOffset * 2) + 1;
            let hitboxY = currY - 29;
            let hitboxW = 24;
            let hitboxH = 29;
        }
        /*
        do Screen.setColor(1);
        do Screen.drawRectangle(
            hitboxX, 
            hitboxY, 
            hitboxX + hitboxW, 
            hitboxY + hitboxH
        );
        //do Sys.wait(2000);
        */
        return;
    }

    method boolean topBounce(Platform p){
        do Output.printInt(555);
        return (
            ((prevY - 29) > (p.getY() + p.getHeight())) |
            ((prevY - 29) = (p.getY() + p.getHeight())) |
            ((Math.abs(currY - prevY) > 100) & ((p.getY() + p.getHeight()) > (currY - 29)))
        );
    }

    method void checkForCollisions(Array Platforms, int duration){
        //if they collide return to previous position
        //and alter direction and dy
        var int iter;
        var Platform p;

        do computeHitbox();
        let iter = 0;
        while(iter < 3){
            let p = Platforms[iter];

            //collision
            if(p.collided(hitboxX, hitboxY, hitboxW, hitboxH))
            {

            if((Math.abs(currY - prevY) < 100) & ((prevY < p.getY()) | (prevY = p.getY()))){
                //adjust currY
                //do Output.printInt(112);
                let currY = p.getY() - 1;
                let airTime = 0;
                let landed = true;
            }else{
                //current values are incorrect
                let currOffset = prevOffset;
                let currX = prevX;
                
                //prev head position is below platform
                if(topBounce(p)){
                    let currY = prevY;
                    //airTime big enough to start falling
                    let airTime = ((-dy + 1) * 60) / gravity;
                    do moveVertical(60);
                }else{
                    //direction = 130 => set direction to 132
                    //direction = 132 => set direction to 130
                    do setDirection(130 + (132 - direction));

                    //increases g for 0.5
                    let airTime = airTime + (30 / gravity);
                }

                if(~(direction = 131)){
                    do moveSideways();
                }

                }
            }
                
            let iter = iter + 1;
        } 

        if((duration = 0) & (~landed)){
            //TODO: 60 IS FPS
            //let airTime = (70 * 60) / 100;
            let gravity = gravity - dy;
            if(airTime < 60){
                let airTime = 60;
            }
            do moveVertical(60);        
        }
        return;
    }

    method void jump(
            int duration, int key, int screenWidth, 
            int FPS, Array Platforms
    ){
        var int frameTime;
        var Platform p;

        let frameTime = Utils.round(10000 / FPS);
        let maxWidth = screenWidth;
        let dy = dy * Utils.round((duration)) / 10; 
        do setDirection(key);
        let landed = false;

        while(~landed){
            do refresh(duration, FPS);

            do checkForCollisions(Platforms, duration);
            
            do Utils.debug(currX, prevX, currOffset, prevOffset, g, g);
            
            do render();    
            
            let p = Platforms[0];
            do p.draw();
            let p = Platforms[1];
            do p.draw();
            let p = Platforms[2];
            do p.draw();
             
            do setDirection(direction);
            do Sys.wait(frameTime);
        }
        
        //reset dy to default
        let dy = -4;
        return; 
    }


    method void dispose(){
        do Memory.deAlloc(debug);
        do Memory.deAlloc(this);
        return;
    }
}
